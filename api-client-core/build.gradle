apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.takipi'
archivesBaseName = 'api-client-core'
version = '0.9'

repositories {
    mavenCentral()
}
dependencies {
	compile(
		"ch.qos.logback:logback-classic:1.1.2",
		"commons-io:commons-io:2.5",
		"com.google.guava:guava:14.0.1",
		"com.google.code.gson:gson:2.8.2",
		"commons-codec:commons-codec:1.9",
	)
}
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}
signing {
    sign configurations.archives
}
uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
      }

      pom.project {
        name 'api-client-core'
        packaging 'jar'
        description 'OverOps API Client core components, used by api-interface project'
        url 'https://github.com/takipi/api-client'

        scm {
          connection 'scm:git:https://github.com/takipi/api-client/tree/master/api-client-core'
          developerConnection 'scm:git:https://github.com/takipi/api-client/tree/master/api-client-core'
          url 'https://github.com/takipi/api-client/tree/master/api-client-core'
        }

        licenses {
          license {
            name 'MIT License'
            url 'http://www.opensource.org/licenses/mit-license.php'
          }
        }

        developers {
          developer {
            id 'chook'
            name 'Chen Harel'
            email 'chen.harel@takipi.com'
          }
        }
      }
    }
  }
}

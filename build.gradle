import org.gradle.api.tasks.testing.logging.TestLogEvent

subprojects {
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'signing'
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  group = 'com.takipi'
  version = '2.34.1'

  repositories {
      mavenCentral()
  }

  task javadocJar(type: Jar) {
      classifier = 'javadoc'
      from javadoc
  }
  task sourcesJar(type: Jar) {
      classifier = 'sources'
      from sourceSets.main.allSource
  }
  artifacts {
      archives javadocJar, sourcesJar
  }
  signing {
      required { gradle.taskGraph.hasTask("uploadArchives") }
      sign configurations.archives
  }

  test {
		useJUnitPlatform()
		
		onlyIf { !Boolean.getBoolean('do.tests') }
		
		testLogging {
			events TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.FAILED, TestLogEvent.STANDARD_ERROR, TestLogEvent.STANDARD_OUT
			exceptionFormat = 'full'
		}
		
		options {
			systemProperties(System.getProperties())
		}
	}
	
	dependencies {
		testCompile(
			'org.junit.jupiter:junit-jupiter-api:5.5.2',
			'org.junit.jupiter:junit-jupiter-params:5.5.2',
			'org.assertj:assertj-core:3.13.2'
		)
		
		testRuntime(
			'org.junit.jupiter:junit-jupiter-engine:5.5.2',
			'ch.qos.logback:logback-classic:1.2.3'
		)
	}

	test.outputs.upToDateWhen { false }

  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
        }
        pom.project {
          packaging 'jar'
          url 'https://github.com/takipi/api-client'

          licenses {
            license {
              name 'MIT License'
              url 'http://www.opensource.org/licenses/mit-license.php'
            }
          }

          developers {
            developer {
              id 'chook'
              name 'Chen Harel'
              email 'chen.harel@takipi.com'
            }
          }
        }
      }
    }
  }
}
